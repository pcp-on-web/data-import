# This Query imports generell attributes for objects pcp:Lecture

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX pcp0: <http://purl.org/pcp-on-web/ontology#> 
PREFIX pcp: <http://pcp-on-web.de/ontology#> 
PREFIX data: <http://data.pcp-on-web.de/v.1/> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>


CONSTRUCT   {

# Lecturer
  ?newsubject rdf:type pcp:Lecturer.
  ?newsubject pcp:derivedFrom ?subject.
  ?newsubject pcp:gnd ?gnd.
  ?newsubject rdfs:label ?label .
  ?newsubject pcp:note ?note .
  ?newsubject pcp:furtherInformation ?furtherInformation .
  ?newsubject ?gender pcp:Gender .
	
  ?newsubject pcp:idPerson ?idPerson .
  ?newsubject pcp:additionToSurname ?additionToSurname .
  ?newsubject pcp:alternativeWritingOfSurname ?alternativeWritingOfSurname .
  ?newsubject pcp:birthname ?birthname .
  ?newsubject pcp:corresponds ?corresponds .
  ?newsubject pcp:forename ?forename .
  ?newsubject pcp:hasEnrollment ?hasEnrollment .
  ?newsubject pcp:hasOrigin ?hasOrigin .
  ?newsubject pcp:isAttributedTo ?isAttributedTo .
  ?newsubject pcp:latinForename ?latinForename .
  ?newsubject pcp:latinSurname ?latinSurname .
  ?newsubject pcp:surname ?surname .	
	
  ?newsubject pcp:hasStageOfLife ?uri_pcp_picture.
  ?uri_pcp_picture rdf:type pcp:Picture.
  ?uri_pcp_picture pcp:pictureLicense ?pictureLicense.
  ?uri_pcp_picture pcp:pictureURL ?pictureURL.

}
WHERE {
  ?subject a pcp0:Professor.
  BIND (IRI(REPLACE(str(?subject),'http://aditus.catalogus-professorum.org/',str(data:))) AS ?newsubject). 
  OPTIONAL {?subject rdfs:label ?label.}
  OPTIONAL {
    	?subject pcp0:gnd ?gnd_source.
    	BIND(
    	# IF(?),wahr, falsch
    	
      		IF ( contains(?gnd_source,"http"), IRI(REPLACE(str(?gnd_source),'http://','https://')),
      			IF (contains(?gnd_source," "),
      				IF (contains(?gnd_source,"(DNB)"), IRI(CONCAT("https://d-nb.info/gnd/",REPLACE(?gnd_source," \\(DNB\\)",""))), ''),
      				IRI(CONCAT("https://d-nb.info/gnd/",?gnd_source)))
        		))  AS ?gnd).
    	FILTER isIRI(?gnd).
    	}
    	
  OPTIONAL {
    	?subject pcp0:note ?note.
  }
  OPTIONAL {
    	?subject pcp0:furtherInformation ?furtherInformation.
  }
  OPTIONAL {
    	?subject pcp0:gender ?gender_source.
    	BIND(
			IF ( contains(?gender_source,"female"), IRI(CONCAT(str(pcp:),'female')),
        		IF (contains(?gender_source,"male"), IRI(CONCAT(str(pcp:),'male')), ''))  AS ?gender).
    	FILTER isIRI(?gender).
  }

  optional {?subject pcp0:idPerson ?idPerson .}
  optional {?subject pcp0:additionToSurname ?additionToSurname .}
  optional {?subject pcp0:alternativeWritingOfSurname ?alternativeWritingOfSurname .}
  optional {?subject pcp0:birthname ?birthname .}
  optional {?subject pcp0:corresponds ?corresponds .}
  optional {?subject pcp0:forename ?forename .}
  optional {?subject pcp0:hasEnrollment ?hasEnrollment .}
  optional {?subject pcp0:hasOrigin ?hasOrigin .}
  optional {?subject pcp0:isAttributedTo ?isAttributedTo .}
  optional {?subject pcp0:latinForename ?latinForename .}
  optional {?subject pcp0:latinSurname ?latinSurname .}
  optional {?subject pcp0:surname ?surname .}

	
	
  OPTIONAL {
    ?subject pcp0:imageSource ?pictureURL.
  	OPTIONAL {?subject pcp0:imageSignature ?imageSignature.}
	BIND (IRI(CONCAT(str(data:), strUUID())) AS ?uri_pcp_picture)
  }
  OPTIONAL {
    ?subject pcp0:hasPicture ?pictureURL.
  	OPTIONAL {?subject pcp0:imageSignature ?imageSignature.}
	BIND (IRI(CONCAT(str(data:), strUUID())) AS ?uri_pcp_picture)
  }
	
}

