# This Query imports generell attributes and stage of life attributs of an Lecturer as basic import

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX pcp0: <http://purl.org/pcp-on-web/ontology#> 
PREFIX pcp: <http://pcp-on-web.de/ontology#> 
PREFIX data: <http://data.pcp-on-web.de/v.1/> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>


CONSTRUCT   {

# Lecturer
  ?newsubject rdfs:label ?label.
  ?newsubject pcp:hasStageOfLife ?uri_pcp_birth.

#Birth
  ?uri_pcp_birth rdf:type pcp:Birth.
  ?uri_pcp_birth pcp:date ?pcp_dateOfBirth.
  ?uri_pcp_birth pcp:stageOfLifeLocation ?pcp_birthplace.
  ?uri_pcp_birth pcp:derivedFrom ?period .
}

WHERE {
  ?subject a pcp0:Professor.
  SERVICE <https://workbench.pcp-on-web.de/store-oker/import-1/query> { 
    ?newsubject pcp:derivedFrom ?subject .
    ?newsubject rdfs:label ?label.
  }
  OPTIONAL {?newsubject rdfs:label ?label.}
  OPTIONAL {
    	?subject pcp0:hasPeriod ?period.
    	?period a pcp0:Birth. 
	OPTIONAL {?period pcp0:date ?pcp_dateOfBirth.}
	OPTIONAL {?period pcp0:periodPlace ?pcp_birthplace.}
	BIND (IRI(CONCAT(str(data:), strUUID())) AS ?uri_pcp_birth)
  }
}

