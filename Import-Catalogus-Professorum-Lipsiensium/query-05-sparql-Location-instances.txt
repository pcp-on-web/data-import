# Import Locations related to periods

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX pcp: <http://pcp-on-web.de/ontology#>
PREFIX data: <http://data.pcp-on-web.de/v.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX cpm: <http://catalogus-professorum.org/cpm/2/>
PREFIX cpl: <http://catalogus-professorum.org/lipsiensium/>


CONSTRUCT   {

?uri_location pcp:derivedFrom ?place .
?uri_location rdf:type ?location_type.

}
WHERE {
  SELECT DISTINCT ?uri_location ?place ?location_type WHERE {
  GRAPH cpl: {  

  ?professor a cpm:Professor.
  ?professor cpm:hasPeriod ?period.
  ?period cpm:periodPlace ?place.
  ?place rdf:type ?location_type.
  
  OPTIONAL {GRAPH data: { ?uri_exists pcp:derivedFrom ?place . }}
  BIND (IF (BOUND(?uri_exists),?uri_exists,IRI(CONCAT(str(data:), strUUID()))) AS ?uri_location)

    }
  }
}
