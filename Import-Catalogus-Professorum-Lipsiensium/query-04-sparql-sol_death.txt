PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX pcp: <http://pcp-on-web.de/ontology#>
PREFIX data: <http://data.pcp-on-web.de/v.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX cpm: <http://catalogus-professorum.org/cpm/2/>
PREFIX cpl: <http://catalogus-professorum.org/>

CONSTRUCT   {
# Lecturer
?lecturer rdf:type pcp:Lecturer.
?lecturer pcp:hasStageOfLife ?uri_period.
?uri_period rdf:type pcp:Death.
?uri_period pcp:derivedFrom ?period.
?uri_period pcp:date ?date.
?uri_period pcp:stageOfLifeLocation ?place.
?place rdfs:label ?place_label.

}
WHERE { SELECT DISTINCT * {
?professor a cpm:Professor.
  # generate new resource for lecture derived from this ?professor
  BIND (IRI(REPLACE(str(?professor),'http://catalogus-professorum.org/',str(data:))) AS ?lecturer).
    #retrieve picture resources
    OPTIONAL {
      ?professor cpm:hasPeriod ?period.
      ?period rdf:type cpm:Death.
      BIND (IRI(CONCAT(str(data:), strUUID())) AS ?uri_period).
      OPTIONAL {?period cpm:date ?date.}
      OPTIONAL {?period cpm:periodPlace ?place.
        OPTIONAL {?place rdfs:label ?place_label.}
      }

    }
  }
}
