# Import Pictures related to lecturer

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX pcp: <http://pcp-on-web.de/ontology#>
PREFIX data: <http://data.pcp-on-web.de/v.1/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX cpm: <http://catalogus-professorum.org/cpm/2/>
PREFIX cpl: <http://catalogus-professorum.org/lipsiensium/>


CONSTRUCT   {

?lecturer pcp:relatedDocument ?uri_pcp_picture.
?uri_pcp_picture pcp:derivedFrom ?picture .
?uri_pcp_picture rdf:type pcp:Picture.

}
WHERE {
  SELECT DISTINCT * WHERE {
  GRAPH cpl: {  

  # Get all professors from CPL catalog and assign a new ressource if new
  ?professor a cpm:Professor.
  ?professor cpm:hasPicture ?picture.

  GRAPH data: { ?lecturer pcp:derivedFrom ?professor . }

  OPTIONAL { SELECT DISTINCT * WHERE {GRAPH data: { ?uri_exists pcp:derivedFrom ?picture . }}}
  BIND (IF (BOUND(?uri_exists),?uri_exists,IRI(CONCAT(str(data:), strUUID()))) AS ?uri_pcp_picture)

    }
  }
}
