PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX pcp0: <http://purl.org/pcp-on-web/ontology#> 
PREFIX pcp: <http://pcp-on-web.de/ontology#> 
PREFIX data: <http://data.pcp-on-web.de/v.1/> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>


CONSTRUCT   {
  ?newsubject rdf:type pcp:Lecturer.
  ?newsubject pcp:gnd ?gnd.
  ?newsubject pcp:Birth ?uri_gnd_birth.
  ?newsubject pcp:Birth ?uri_pcp_birth.
  ?newsubject rdfs:label ?label .
  ?uri_gnd_birth pcp:date ?gnd_dateOfBirth.
  ?uri_gnd_birth pcp:stageOfLifeLocation ?gnd_placeOfBirth.
  ?uri_gnd_birth pcp:derivedFrom data:dataset_gnd .
  ?uri_pcp_birth pcp:date ?pcp_dateOfBirth.
  ?uri_pcp_birth pcp:stageOfLifeLocation ?pcp_birthplace.
  ?uri_pcp_birth pcp:derivedFrom data:dataset_pcp .
}
WHERE {
  ?subject a pcp0:Professor.
  BIND (IRI(REPLACE(str(?subject),'http://aditus.catalogus-professorum.org/',str(data:))) AS ?newsubject). 
  OPTIONAL {?subject rdfs:label ?label.}
  OPTIONAL {
    	?subject pcp0:gnd ?gnd.
		?gnd <http://d-nb.info/standards/elementset/gnd#dateOfBirth> ?gnd_dateOfBirth.
		OPTIONAL {?gnd <http://d-nb.info/standards/elementset/gnd#placeOfBirth> ?gnd_placeOfBirth.}
    	BIND (IRI(CONCAT(str(data:), strUUID())) AS ?uri_gnd_birth)
  }
# FROM SOURCES
  OPTIONAL {
    	?subject pcp0:hasPeriod ?period.
    	?period a pcp0:Birth. 
		  OPTIONAL {?period pcp0:date ?pcp_dateOfBirth.}
		  OPTIONAL {?period pcp0:periodPlace ?pcp_birthplace.}
		  }
      	BIND (IRI(CONCAT(str(data:), strUUID())) AS ?uri_pcp_birth)
}

